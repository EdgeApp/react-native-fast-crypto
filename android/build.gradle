buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.2'
  }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def DEFAULT_COMPILE_SDK_VERSION = 34
def DEFAULT_BUILD_TOOLS_VERSION = '34.0.0'
def DEFAULT_MIN_SDK_VERSION = 24
def DEFAULT_TARGET_SDK_VERSION = 34

android {
  compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
  buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)
  ndkVersion safeExtGet('ndkVersion', '28.0.12674087')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
    targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
    versionCode 1
    versionName '1.0'
    
    ndk {
      abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
    }
    
    externalNativeBuild {
      cmake {
        cppFlags "-D_FILE_OFFSET_BITS=64"
        arguments "-DANDROID_ARM_NEON=TRUE"
      }
    }
  }
  lintOptions {
    abortOnError false
  }
  externalNativeBuild {
    cmake {
      path "src/main/cpp/CMakeLists.txt"
    }
  }

  // Older vesions of the Android Gradle plugin need us to manually add
  // the static libraries to the APK:
  if (com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION < '4.0') {
    sourceSets {
      main {
        jniLibs.srcDirs 'jni/libs/'
      }
    }
  }
}

repositories {
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
}
